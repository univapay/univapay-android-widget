apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = GROUP_NAME
version = VERSION_NAME

task sourceJar(type: Jar){

    from android.sourceSets.main.java.srcDirs
    classifier "source"
}


android.libraryVariants.all { variant ->
    variant.outputs.all { output ->
        publishing.publications.create(variant.name, MavenPublication) {
            artifact sourceJar

            artifact source: output.outputFile, classifier: output.name

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')

                configurations.getByName(variant.name + "CompileClasspath").allDependencies
                        .findAll { it instanceof ExternalDependency }
                        .each {
                    def dependency = dependencies.appendNode('dependency')

                    dependency.appendNode('groupId', it.group)
                    dependency.appendNode('artifactId', it.name)
                    dependency.appendNode('version', it.version)
                }
            }
        }
    }
}

tasks.all {task->
    if(task instanceof AbstractPublishToMaven){
        task.dependsOn assemble
    }
}

publishing {
    repositories {
        maven {
            url S3_BUCKET
            authentication {
                awsIam(AwsImAuthentication)
            }
        }
    }
}
